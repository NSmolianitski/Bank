buildscript {
	dependencies {
		classpath 'org.postgresql:postgresql:42.6.0'
	}
	repositories {
		mavenCentral()
	}
}

plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.2'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'org.flywaydb.flyway' version '8.2.0'
	id 'org.jooq.jooq-codegen-gradle' version '3.19.3'
}

group = 'co.baboon'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework:spring-jdbc'
	implementation 'org.springframework.boot:spring-boot-starter-security'

	implementation 'io.jsonwebtoken:jjwt-api:0.12.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-gson:0.12.5'
	
	implementation 'org.flywaydb:flyway-core'
	
	implementation 'org.jooq:jooq:3.19.3'
	implementation 'com.walterjwhite.java.dependencies:hikari-jdbc-connection-pool:0.0.17'
	
	implementation 'org.joda:joda-money:1.0.4'
	
	runtimeOnly 'org.postgresql:postgresql'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

flyway {
	url = 'jdbc:postgresql://localhost:5432/BankDb'
	user = 'admin'
	password = 'admin'
	locations = ['classpath:db/migration']
	validateMigrationNaming = true
}

flywayMigrate.dependsOn classes

jooq {
	configuration {
		jdbc {
			url = 'jdbc:postgresql://localhost:5432/BankDb'
			user = 'admin'
			password = 'admin'
		}
		generator {
			database {
				inputSchema = 'public'
			}
			target {
				packageName = 'co.baboon.bank.jooq'
				directory = "$buildDir/generated/sources/jooq"
			}
		}
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

sourceSets {
	main {
		java {
			srcDirs += "$buildDir/generated/sources/jooq"
		}
	}
}
